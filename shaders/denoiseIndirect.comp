#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_ray_query : enable
#extension GL_ARB_shader_clock : enable
#extension GL_EXT_shader_image_load_formatted : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_GOOGLE_include_directive : enable

#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require

#include "denoise.glsl"

layout(local_size_x = BlockSizeX, local_size_y = BlockSizeY) in;

ivec2 indSize() {
    return rtxState.size / 2;
}

vec3 waveletFilter(image2D inImage, ivec2 coord, vec3 norm, vec3 pos, uint matHash, float sigLumin, float sigNormal, float sigDepth, int level) {
    if (matHash == InvalidMatId) {
        return vec3(0.0);
    }
    int step = 1 << level;

    vec3 sum = vec3(0.0);
    float sumWeight = 0.0;

    vec3 color = imageLoad(inImage, coord).rgb;

    for (int j = -2; j <= 2; j++) {
        for (int i = -2; i <= 2; i++) {
            ivec2 q = coord + ivec2(i, j) * step;
            if (q.x >= indSize().x || q.y >= indSize().y ||
                q.x < 0 || q.y < 0) {
                continue;
            }

            vec3 normQ;
            vec3 posQ;
            uint matHashQ;

            loadThisGeometry(q * 2, normQ, posQ, matHashQ, indSize());
            vec3 colorQ = imageLoad(inImage, q).rgb;

            if (matHash != matHashQ || matHashQ == InvalidMatId) {
                continue;
            }

            float var = sigLumin;
            float distColor = dot(color - colorQ, color - colorQ);
            float wColor = exp(-distColor / var) + 1e-2;

            float distNorm2 = dot(norm - normQ, norm - normQ);
            float wNorm = min(1.0, exp(-distNorm2 / sigNormal));

            float distPos2 = dot(pos - posQ, pos - posQ);
            float wDepth = exp(-distPos2 / sigDepth) + 1e-2;

            float weight = wColor * wNorm * wDepth * Gaussian5x5[i + 2][j + 2];
            sum += colorQ * weight;
            sumWeight += weight;
        }
    }
    vec3 res = (sumWeight < 1e-5) ? vec3(0.0) : sum / sumWeight;
    if (hasNan(res) || res.x < 0 || res.y < 0 || res.z < 0 ||
        res.x > 1e8 || res.y > 1e8 || res.z > 1e8) {
        res = vec3(0.0);
    }
    return res;
}

void main() {
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    if (coord.x >= indSize().x || coord.y >= indSize().y || rtxState.denoise == 0) {
        return;
    }

    vec3 albedo;
    vec3 norm;
    vec3 pos;
    uint matHash;
    loadThisGeometry(coord * 2, norm, pos, matHash, indSize());

    if (rtxState.denoiseLevel == 0) {
        vec3 res = waveletFilter(denoiseIndTempA, coord, norm, pos, matHash, rtxState.sigLuminIndirect, rtxState.sigNormalIndirect, rtxState.sigDepthIndirect, 0);
        imageStore(denoiseIndTempB, coord, vec4(res, 1.0));
    }
    else if (rtxState.denoiseLevel == 1) {
        vec3 res = waveletFilter(denoiseIndTempB, coord, norm, pos, matHash, rtxState.sigLuminIndirect, rtxState.sigNormalIndirect, rtxState.sigDepthIndirect, 1);
        imageStore(denoiseIndTempA, coord, vec4(res, 1.0));
    }
    else if (rtxState.denoiseLevel == 2) {
        vec3 res = waveletFilter(denoiseIndTempA, coord, norm, pos, matHash, rtxState.sigLuminIndirect, rtxState.sigNormalIndirect, rtxState.sigDepthIndirect, 2);
        imageStore(IndirectResultImage, coord, vec4(res, 1.0));
    }
    else if (rtxState.denoiseLevel == 3) {
        vec3 res = waveletFilter(IndirectResultImage, coord, norm, pos, matHash, rtxState.sigLuminIndirect, rtxState.sigNormalIndirect, rtxState.sigDepthIndirect, 3);
        imageStore(denoiseIndTempA, coord, vec4(res, 1.0));
    }
    else if (rtxState.denoiseLevel == 4) {
        vec3 res = waveletFilter(denoiseIndTempA, coord, norm, pos, matHash, rtxState.sigLuminIndirect, rtxState.sigNormalIndirect, rtxState.sigDepthIndirect, 4);
        res = LDRToHDR(res);
        imageStore(denoiseIndTempB, coord, vec4(res, 1.0));
    }
}